# Maintainer: Hao Long <aur@esd.cc>

pkgname=archivebox
pkgver=0.7.1
pkgrel=1
pkgdesc="The open source self-hosted web archive"
arch=("any")
url="https://github.com/pirate/ArchiveBox"
license=('MIT')
provides=('archivebox')
conflicts=('archivebox')
depends=('python-django' 'python-mypy_extensions' 'wget'
  'python-python-crontab' 'python-atomicwrites' 'python-dateparser'
  'python-requests' 'python-base32_crockford' 'python-w3lib'
  'chromium' 'python-django-extensions' 'git'
  'yt-dlp' 'curl' 'single-file-git' 'python-pdm'
  'readability-extractor' 'ripgrep' 'mercury-parser')
makedepends=('python-build' 'python-installer' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
b2sums=('e0ed49770a56c52b3fc31f66d8f0c68b3a335ac3efb3e11c2bac754527d9c136abafeccd7fbf228dac6ae024156644b1bb440cc70e4994b0dc57af27e7283e70')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 archivebox/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
